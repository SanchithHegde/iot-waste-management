buildscript {
    // Define versions in a single place
    ext {
        // SDK and tools
        buildToolsVersion = "30.0.3"
        compileSdkVersion = 30
        minSdkVersion = 26
        targetSdkVersion = 30

        // Top-level Gradle plugins
        oneSignalGradlePluginVersion = "0.12.10"

        // App dependencies
        appCompatVersion = "1.3.0"
        constraintLayoutVersion = "2.0.4"
        gradleVersion = "4.2.1"
        hiltVersion = "2.36"
        hiltViewModelVersion = "1.0.0-alpha03"
        kotlinVersion = "1.5.10"
        ktfmtVersion = "0.25"
        ktlintVersion = "0.38.1"
        ktxVersion = "1.5.0"
        lifecycleVersion = "2.3.1"
        materialVersion = "1.3.0"
        navigationVersion = "2.3.5"
        oneSignalVersion = "4.4.0"
        preferenceVersion = "1.1.1"
        roomVersion = "2.3.0"
    }

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"
        classpath "gradle.plugin.com.onesignal:onesignal-gradle-plugin:$oneSignalGradlePluginVersion"
    }
}

plugins {
    id "com.diffplug.spotless" version "5.12.5"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

spotless {
    kotlin {
        encoding "UTF-8"
        target "**/*.kt"
        targetExclude "**/build/"
        ktlint(ktlintVersion).userData(["indent_size": "2", "continuation_indent_size": 2])
        ktfmt(ktfmtVersion).googleStyle()

        endWithNewline()
        trimTrailingWhitespace()
    }

    format "xml", {
        encoding "UTF-8"
        target "**/*.xml"
        targetExclude "**/build/*", ".idea/"

        endWithNewline()
        indentWithSpaces(2)
        trimTrailingWhitespace()
    }
}
